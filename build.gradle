buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'cz.auderis:auderis-gradle-tools:1.0.1'
	}
}

/*
 * Copyright 2017 Boleslav Bobcik - Auderis
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import cz.auderis.tools.gradle.SemanticVersion

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'signing'

project.group = 'cz.auderis'
project.version = SemanticVersion.from("version.txt")

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
	mavenCentral()
}

dependencies {
	testCompile('pl.pragmatists:JUnitParams:1.0.5') {
		exclude group: 'junit'
	}
	testCompile('junit:junit:4.12') {
		exclude group: 'org.hamcrest'
	}
	testCompile 'org.hamcrest:hamcrest-all:1.3'
	testCompile 'cz.auderis:auderis-test-category:1.0.1'
	testCompile 'cz.auderis:auderis-test-extra:1.2.3'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
	from sourceSets.main.allSource
	classifier = 'sources'
}

artifacts {
	archives jar
	archives javadocJar
	archives sourcesJar
}


if (version.stable) {
	if (!hasProperty("releaseRepository")) {
		throw new InvalidUserDataException("Property 'releaseRepository' is not defined")
	}
	if (!LOCAL_CREDENTIALS.sonatype) {
		throw new InvalidUserDataException("Sonatype credentials not defined (check LOCAL_CREDENTIALS.sonatype definition)")
		/**
		 * Recommended setup for credentials: create file ~/.gradle/init.d/credentials.gradle with the following
		 * contents:
		 * <pre>
		 *   rootProject {
		 *     ext.LOCAL_CREDENTIALS = [
		 *       sonatype: [
		 *         userName: '...',
		 *         password: '...'
		 *       ],
		 *     ]
		 *   }
		 * </pre>
		 */
	}
	signing {
		sign configurations.archives
	}
} else {
	task signArchives << {
		logger.info("Non-release build, signing not performed")
	}
}

uploadArchives {
	repositories {
		if (version.stable) {
			mavenDeployer {
				beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

				repository(url: releaseRepository) {
					authentication(userName: LOCAL_CREDENTIALS.sonatype.userName, password: LOCAL_CREDENTIALS.sonatype.password)
				}

				pom.project {
					name 'Auderis BinBuilder'
					description 'Auderis BinBuilder generates binary content declaratively'
					url 'https://github.com/bbobcik/auderis-binbuilder'
					inceptionYear 2017
					packaging 'jar'

					organization {
						name 'Boleslav Bobcik - Auderis'
					}

					developers {
						developer {
							name 'Boleslav Bobcik'
							email 'bbobcik@gmail.com'
							organization 'Auderis'
							organizationUrl 'https://github.com/bbobcik'
							timezone 1
						}
					}

					licenses {
						license {
							name 'The Apache Software License, Version 2.0'
							url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
						}
					}

					scm {
						url 'git@github.com:bbobcik/auderis-binbuilder.git'
						connection 'scm:git:git@github.com:bbobcik/auderis-binbuilder.git'
						developerConnection 'scm:git:git@github.com:bbobcik/auderis-binbuilder.git'
						tag 'HEAD'
					}
				}
			}
		} else {
			mavenLocal()
		}
	}
}

uploadArchives.doFirst {
	if (releaseBuild) {
		logger.lifecycle("Release build - uploading artifacts to OSS Sonatype Nexus")
	} else {
		logger.lifecycle("Non-release build - uploading artifacts to Maven Local")
	}
}
